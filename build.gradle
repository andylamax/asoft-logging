buildscript {
    ext {
        kotlin_version = '1.3.50'
        serializatoin_version = "0.12.0"
        coroutines_version = "1.3.0"
        klock_version = "1.6.0"
        persist_version = "3.2.2"
    }

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

plugins {
    id 'kotlin-multiplatform' version '1.3.50'
}

tasks.whenTaskAdded { task ->
    if (task.name.contains("install")) {
//        task.enabled = false
    }
}

group 'tz.co.asoft'
version '2.3.0'

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'kotlinx-serialization'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    google()
    mavenCentral()
    jcenter()
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-js-wrappers' }
    maven { url 'https://dl.bintray.com/kotlinx/kotlinx' }
    maven { url "https://jitpack.io" }
}

kotlin {
    jvm() {
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }

    android() {
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }

    js() {
        browser {
            testTask {
                useKarma {
                    useChrome()
                }
            }
        }
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                kotlinOptions.metaInfo = true
                kotlinOptions.sourceMap = true
                kotlinOptions.moduleKind = 'commonjs'
                kotlinOptions.main = "call"
            }
        }
    }

    js("node") {
        nodejs()
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                kotlinOptions.metaInfo = true
                kotlinOptions.sourceMap = true
                kotlinOptions.moduleKind = 'commonjs'
                kotlinOptions.main = "call"
            }
        }
    }

    android {
        publishLibraryVariants("release")
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serializatoin_version"
                implementation "com.soywiz.korlibs.klock:klock-metadata:$klock_version"
                implementation "com.github.andylamax.asoft-persist:asoft-persist-metadata:$persist_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
            }
        }

        commonTest {
            dependsOn commonMain
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')

            }
        }

        jvmCommonMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serializatoin_version"
            }
        }
        jvmCommonTest {
            dependsOn jvmCommonMain
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }

        jvmMain {
            dependsOn jvmCommonMain
            dependencies {
                implementation "com.github.andylamax.asoft-persist:asoft-persist-jvm:$persist_version"
                implementation "com.soywiz.korlibs.klock:klock-jvm:$klock_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
            }
        }

        jvmTest {
            dependsOn jvmMain
            dependsOn jvmCommonTest
        }

        androidMain {
            dependsOn jvmCommonMain
            dependencies {
                implementation "com.github.andylamax.asoft-persist:asoft-persist-android:$persist_version"
                implementation "com.soywiz.korlibs.klock:klock-android:$klock_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
            }
        }

        androidTest {
            dependsOn androidMain
            dependsOn jvmCommonTest
        }

        jsCommonMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "com.soywiz.korlibs.klock:klock-js:$klock_version"
                implementation "com.github.andylamax.asoft-persist:asoft-persist-js:$persist_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serializatoin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$coroutines_version"
            }
        }

        jsCommonTest {
            dependsOn jsCommonMain
            dependencies {
                implementation kotlin('test-js')
            }
        }

        jsMain {
            dependsOn jsCommonMain
        }

        jsTest {
            dependsOn jsMain
            dependsOn jsCommonTest
            dependencies {
                implementation kotlin('test-js')
            }
        }

        nodeMain {
            dependsOn jsCommonMain
        }

        nodeTest {
            dependsOn nodeMain
            dependsOn jsCommonTest
        }
    }
}

configurations {
    compileClasspath
}